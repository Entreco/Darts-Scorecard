apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../scripts/versioning.gradle'
apply from: '../scripts/coverage.gradle'

android {
    def config = rootProject.extensions.getByName("ext")
    compileSdkVersion config.androidCompileSdkVersion
    buildToolsVersion config.androidBuildToolsVersion

    defaultConfig {
        applicationId "nl.entreco.dartsscorecard"
        minSdkVersion config.androidMinSdkVersion
        targetSdkVersion config.androidTargetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        resConfigs "en", "nl"

        resValue "string", "version", "${config.versionName}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        htmlReport true
        htmlOutput file("lint-report.html")
        abortOnError false
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {

    implementation project(":domain")
    implementation project(":data")

    // Annotation Processing
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.android.databinding:compiler:$gradleVersion"
    kapt "android.arch.persistence.room:compiler:$architectureComponents"

    // Implementation
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support.constraint:constraint-layout:$constraint"
    implementation "com.google.firebase:firebase-core:$firebase"
    implementation "com.google.android.gms:play-services-ads:$firebase"
    implementation "android.arch.lifecycle:extensions:$architectureComponents"
    implementation "android.arch.persistence.room:runtime:$architectureComponents"

    // Test
    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlin"
    testImplementation "com.github.dpreussler:android-tdd-utils:$tddUtils"

    // AndroidTest
    androidTestImplementation "com.android.support.test:runner:$espressoRunner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoCore"
}

apply plugin: 'com.google.gms.google-services'


